#include <util/crc16.h>
#include "Arduino.h"
#include "Wire.h"

void setup() {
    delay(5000);
    Wire.begin();
}

#define page_size %d
#define frame_size %d
#define blank 0x%x
#define pages %d
#define firmware_length %d
#define DELAY %d
#define LEFT_ADDRESS 0x50
#define RIGHT_ADDRESS 0x53


#define ENDTRANS_SUCCESS 0
#define ENDTRANS_DATA_TOO_LONG 1
#define ENDTRANS_ADDR_NACK 2
#define ENDTRANS_DATA_NACK 3
#define ENDTRANS_ERROR 4

uint16_t offsets[pages] = {%s};
const byte firmware[firmware_length] PROGMEM = {%s};

int left_written = 0;
int right_written = 0;

// The LEFT ATTiny has a reset pin directly connected to the ATMega
void reset_left_attiny() {
    // Hold the left ATTiny in reset,
    DDRC |= _BV(6);
    PORTC &= ~_BV(6);
    delay(30);
    DDRC &= ~_BV(6); // Turn the ATTiny back on
}

// The RIGHT ATTiny is on the other side of the wired i2c bus.
// Our best chance at resetting it is by toggling the current limiter.

void reset_right_attiny() {
    // Hold the left ATTiny in reset,
    DDRC |= _BV(7);
    PORTC &= ~_BV(7);
    delay(1000);
    PORTC |= _BV(7); // Turn the ATTiny back on
}
void print_result(byte result) {
    print("result = ");
    print(result);
    print("\n");
}

int run_command(byte address, byte command) {

    Wire.beginTransmission(address);
    Wire.write(command); // erase user space
    Wire.write(0x00); // dummy end byte
    byte result = Wire.endTransmission();
    print_result(result);
    return result;
}



byte read_crc16(byte addr, byte *version, uint16_t *crc16, uint16_t offset, uint16_t length) {
    byte result = 2;

    Wire.beginTransmission(addr);
    Wire.write(0x06); // get version and CRC16
    Wire.write(offset & 0xff); // addr (lo)
    Wire.write(offset >> 8); // addr (hi)
    Wire.write(length & 0xff); // len (lo)
    Wire.write(length >> 8); // len (hi)
    result = Wire.endTransmission();
    if (result != 0) {
        return result;
    }
    Wire.requestFrom(addr, (uint8_t) 3);
    print("Available bytes: ");
    print(Wire.available());
    print("\n");
    if (Wire.available() == 0) {
    }
    byte v = Wire.read();
    *version = v;
    if (Wire.available() == 0) {
        return 0xFF;
    }
    byte crc16_lo = Wire.read();
    if (Wire.available() == 0) {
        return 0xFF;
    }
    byte crc16_hi = Wire.read();
    while (Wire.available()) {
        byte c = Wire.read();
    }
    *crc16 = (crc16_hi << 8) | crc16_lo;
    return result;
}


void get_version (byte addr) {

    byte result = 2;
    while (result != 0) {
        print("Reading CRC16...");

        byte version;
        uint16_t crc16;
        result = read_crc16(addr, &version, &crc16, 0, firmware_length);

        print("result ");
        print(result);
        print("\n");
        if (result != 0) {
            _delay_ms(100);
            continue;
        }
        print("Version: ");
        print(version);
        print("\n");
        print("Existing CRC16 of 0000-1FFF: ");
        print(crc16, HEX);
        print("\n");
    }

}



int erase_program(byte addr) {

    print("Erasing\n");
    Wire.beginTransmission(addr);
    Wire.write(0x04); // erase user space
    byte result = Wire.endTransmission();
    print("result = ");
    print(result);
    print("\n");
    if (result != 0) {
        _delay_ms(1000);
        return -1;
    }
    return 0;

}


int write_firmware(byte addr ) {

    byte result = 3;
    byte o = 0;

    for (uint16_t i = 0; i < firmware_length; i += page_size) {
        print("Page ");
        print(offsets[o]);
        print(" setting address...");
        Wire.beginTransmission(addr);
        Wire.write(0x1); // write page addr
        Wire.write(offsets[o] & 0xff); // write page addr
        Wire.write(offsets[o] >> 8);
        result = Wire.endTransmission();
        _delay_ms(DELAY);
        // got something other than ACK. Start over.
        if (result != 0) {
            print("Error setting the page address\n");
            return -1;
        }

        // transmit each frame separately

        for (uint8_t frame = 0; frame < page_size / frame_size; frame++) {
            Wire.beginTransmission(addr);
            Wire.write(0x2); // continue page
            uint16_t crc16 = 0xffff;
            for (uint8_t j = frame * frame_size; j < (frame + 1) * frame_size; j++) {
                if (i + j < firmware_length) {
                    uint8_t b = pgm_read_byte(&firmware[i + j]);
                    Wire.write(b);
                    crc16 = _crc16_update(crc16, b);
                } else {
                    Wire.write(blank);
                    crc16 = _crc16_update(crc16, blank);
                }
            }
            // write the CRC16, little end first
            Wire.write(crc16 & 0xff);
            Wire.write(crc16 >> 8);
            Wire.write(0x00); // dummy end byte
            result = Wire.endTransmission();
            print(": frame ");
            print(frame);
            print(" = ");
            print(result);
            // got something other than NACK. Start over.
            if (result != 3) {
                print("Error: Got something other than NACK\n");
                return -1;
            }
            delay(DELAY);
        }
        o++;
        print("\n");
    }
    return 0;
}


int verify_firmware(byte addr) {
    byte result = 3;
    // verify firmware
    print("## Verifying firmware installation\n");
    while (result != 0) {
        print("CRC16: ");

        byte version;
        uint16_t crc16;
        // skip the first 4 bytes, are they were probably overwritten by the reset vector preservation
        result = read_crc16(addr, &version, &crc16, offsets[0] + 4, firmware_length - 4);

        print(result);
        print("\n");
        if (result != 0) {
            _delay_ms(100);
            continue;
        }
        print("Version: ");
        print(version);
        print("\n");
        print("CRC CRC16 of ");
        print(offsets[0] + 4, HEX);
        print("-");
        print(offsets[0] + firmware_length, HEX);
        print(": ");
        print(crc16, HEX);
        print("\n");
        // calculate our own CRC16
        uint16_t check_crc16 = 0xffff;
        for (uint16_t i = 4; i < firmware_length; i++) {
            check_crc16 = _crc16_update(check_crc16, pgm_read_byte(&firmware[i]));
        }
        if (crc16 != check_crc16) {
            print("CRC does not match ours: ");
            print(check_crc16, HEX);
            print("\n");
            return -1;
        }
        print("CRC check: OK\n");
    }
    return 0;
}

byte update_attiny(byte addr) {
    print("Communicating");
    print("\n");
    get_version(addr);

    int erased = erase_program(addr);

    if (erased == -1) {

        print("Erase failed.\n");
        return 0;
    }

    int firmware_written = write_firmware(addr);
    if(firmware_written == -1) {
        print("Firmware write failed.\n");
        return 0;
    }

    int firmware_verified = verify_firmware(addr);
    if(firmware_verified == -1) {
        print("Firmware verify failed");
        return 0;
    }

    print("Resetting ATTiny\n");
    int result = run_command(addr, 0x03); // execute app
    print("Done!\n");

    return 1;
}

void loop() {

    delay(5000);


    if (left_written > 0 && right_written > 0 ) {

        print("Both ATTiny MCUs have been flashed\n");
        print("It is now safe to reflash your keyboard with regular firmware\n");
        return;
    }

    print("Starting!\n");

    if (left_written > 0) {
        print("Already done with the left hand.\n");
        // we're done
    } else {
        reset_left_attiny();
        left_written = update_attiny(LEFT_ADDRESS);

    }

    if (right_written > 0) {
        print("Already done with the right hand.\n");
        print(right_written);
        print("\n");
        // we're done
    } else {
        reset_right_attiny();
        right_written = update_attiny(RIGHT_ADDRESS);
    }



}


void print(char str[]) {
  Serial.print(str);
}
void print(byte str) {
  Serial.print(str);
}

void print(const char str[]) {
  Serial.print(str);
}
void print(uint16_t str, int conversion) {
  Serial.print(str, conversion);
}

